plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group "ru.enis"
version String.format("1.7.3%s-rev.%s", getBranchVer(), getBranchHash())

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    
    mavenCentral()
    mavenLocal()
    
    maven { url = "https://jitpack.io" }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url = "https://repo.kryptonmc.org/releases" }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}
dependencies {
    implementation "org.bstats:bstats-bukkit:3.0.0"
    implementation "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation "net.kyori:adventure-text-serializer-legacy:4.11.0"
    implementation "net.kyori:adventure-text-minimessage:4.11.0"
    implementation "com.github.Elytrium:ElytriumJavaCommons:1.0.3"
    implementation "org.yaml:snakeyaml:1.29"
    implementation "org.projectlombok:lombok:1.18.20"

    compileOnly "org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT"
    compileOnly "org.projectlombok:lombok:1.18.24"
    compileOnly "org.jetbrains:annotations:22.0.0"
    compileOnly "me.clip:placeholderapi:2.11.2"

    annotationProcessor "org.projectlombok:lombok:1.18.24"
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

shadowJar {
    relocate "org.bstats", "ru.enis.metrics"
    relocate "net.kyori", "ru.enis.libs.kyori"

    archiveFileName = "${rootProject.name}-${version}.${extension}"
}
java {
    withSourcesJar()
}
sourceSets {
    main {
        resources {
            srcDir "resources"
        }
        java {
            srcDir "spigot"
        }
    }
}

def getBranchHash() {
    def branchHash = "git rev-parse --short HEAD".execute().text.trim()
    return branchHash
}

def getBranchVer() {
    def branchName = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
    if (branchName.startsWith("dev")) {
        return "-DEV"
    } else {
        return ""
    }
}
